buildscript {
	dependencies {
		classpath "org.kohsuke:github-api:1.327"
	}
}

plugins {
	id "fabric-loom" version "1.10-SNAPSHOT"
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
	id "com.diffplug.spotless" version "7.0.2"
}

def ENV = System.getenv()

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

// Override vulnerable dependencies until Minecraft updates to newer versions
configurations.all {
	resolutionStrategy {
		// v2.5.1, used by Minecraft 1.21.4 - 1.21.5, is vulnerable to CVE-2024-57699
		force "net.minidev:json-smart:2.5.2"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/mo-glass.accesswidener")
}

configurations {
	productionRuntime {
		extendsFrom configurations.minecraftLibraries
		extendsFrom configurations.loaderLibraries
		extendsFrom configurations.minecraftRuntimeLibraries
	}
}

dependencies {
	productionRuntime "net.fabricmc:fabric-loader:${project.loader_version}"
	productionRuntime "net.fabricmc:intermediary:${project.minecraft_version}"
}

import net.fabricmc.loom.util.Platform
tasks.register("runEndToEndTest", JavaExec) {
	dependsOn modrinthJar, downloadAssets
	classpath.from configurations.productionRuntime
	mainClass = "net.fabricmc.loader.impl.launch.knot.KnotClient"
	workingDir = file("run")
	
	doFirst {
		classpath.from loom.minecraftProvider.minecraftClientJar
		workingDir.mkdirs()
		
		args(
			"--assetIndex", loom.minecraftProvider.versionInfo.assetIndex().fabricId(loom.minecraftProvider.minecraftVersion()),
			"--assetsDir", new File(loom.files.userCache, "assets").absolutePath,
			"--gameDir", workingDir.absolutePath
		)
		
		if (Platform.CURRENT.operatingSystem.isMacOS()) {
			jvmArgs("-XstartOnFirstThread")
		}
		
		jvmArgs(
			"-Dfabric.addMods=${configurations.modImplementation.find { it.name.contains('fabric-api') }.absolutePath}${File.pathSeparator}${modrinthJar.archiveFile.get().asFile.absolutePath}",
			"-Dmo_glass.e2eTest",
			// TODO: translate the tags and then re-enable this
			// "-Dfabric-tag-conventions-v2.missingTagTranslationWarning=fail",
			"-Dfabric-tag-conventions-v1.legacyTagWarning=fail",
			"-Dmixin.debug.verify=true",
			"-Dmixin.debug.countInjections=true"
		)
	}
}

tasks.named("processResources") {
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.20.5 (24w14a) upwards uses Java 21.
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
	
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

tasks.named("jar") {
	inputs.property "archivesName", project.base.archivesName
	
	from("LICENSE") {
		rename {"${it}_${inputs.properties.archivesName}"}
	}
	
	exclude("/assets/mo_glass/textures/block/*.psd")
	exclude("intentionally_untranslated.json")
}

import com.diffplug.spotless.generic.LicenseHeaderStep
spotless {
	lineEndings = "WINDOWS"
	java {
		removeUnusedImports()
		leadingSpacesToTabs()
		trimTrailingWhitespace()
		eclipse().configFile(file("codestyle/formatter.xml"))
	}
	format("licenseHeader") {
		target("src/*/java/**/*.java", "src/test/java/**/*.java")
		def header_file = file("codestyle/license_header.txt")
		def delimiter = LicenseHeaderStep.DEFAULT_JAVA_HEADER_DELIMITER
		licenseHeaderFile(header_file, delimiter).updateYearWithLatest(true)
	}
	json {
		target "src/**/*.json"
		gson().indentWithSpaces(2).version("2.11.0")
	}
}

import groovy.json.JsonSlurper
import groovy.json.JsonBuilder
def createPlatformJar(String platform) {
	def simplePlatform = platform.toLowerCase().replace("-", "")
	tasks.register("${simplePlatform}Jar", Jar) {
		dependsOn remapJar
		
		archiveBaseName = base.archivesName.get()
		archiveVersion = project.version
		archiveClassifier = platform
		destinationDirectory = file("${project.buildDir}/libs")
		
		preserveFileTimestamps = false
		reproducibleFileOrder = true
		
		doFirst {
			def originalJar = remapJar.archiveFile.get().asFile
			
			// Get the manifest from the original jar
			zipTree(originalJar).matching { include 'META-INF/MANIFEST.MF' }
				.each { manifestFile -> manifest.from(manifestFile) }
			
			// Extract original JAR into a temp directory
			def tempDir = file("${buildDir}/tmp/${simplePlatform}Jar")
			tempDir.deleteDir()
			tempDir.mkdirs()
			copy {
				from zipTree(originalJar)
				into tempDir
			}
			
			// Modify fabric.mod.json
			def fabricJsonFile = new File(tempDir, "fabric.mod.json")
			def json = new JsonSlurper().parseText(fabricJsonFile.text)
			
			// Set the platform property
			if (json.custom == null)
				json.custom = [:]
			if (json.custom.mo_glass == null)
				json.custom.mo_glass = [:]
			json.custom.mo_glass.platform = platform
			
			// Convert back to JSON
			fabricJsonFile.text = new JsonBuilder(json)
				.toPrettyString().replaceAll("    ", "  ") + "\n"
			
			// Build the platform jar
			from(tempDir)
		}
	}
}

def modrinthJar = createPlatformJar("Modrinth")
def curseforgeJar = createPlatformJar("CurseForge")
def wimodsJar = createPlatformJar("WI-Mods")

def getGhVersion() {
	return "v" + version.substring(0, version.indexOf("-MC"))
}

def getChangelogLink(String platform, String from) {
	def modSlug = project.archives_base_name.toLowerCase()
	def versionSlug = getGhVersion().substring(1).replace(".", "-")
	def shortURL = "https://www.wimods.net/${modSlug}/${modSlug}-${versionSlug}/"
	def query = []
	if (platform != "GitHub")
		query.add("mc=${project.minecraft_version}")
	query.add("utm_source=${platform}")
	query.add("utm_medium=" + project.archives_base_name.replace("-", "+"))
	def fullURL = shortURL + (query.isEmpty() ? "" : "?" + query.join("&"))
	def encodedURL = java.net.URLEncoder.encode(fullURL, "UTF-8")
	return "[${shortURL}](https://go.wimods.net/from/${from}?to=${encodedURL})"
}

def getChangelogImage(String from) {
	return "![](https://img.wimods.net/${from})"
}

def getChangelog(String platform, String from) {
	def prefix = platform == "GitHub" ? "" : "A changelog can be found at: "
	def imgQuery = platform == "GitHub" ? "" : "?mc_version=${project.minecraft_version}&mod_loader=${project.mod_loader}"
	return prefix + getChangelogLink(platform, from) + getChangelogImage(from + imgQuery)
}

publishMods {
	def version = project.version
	def archivesName = base.archivesName.get()
	def modSlug = archivesName.toLowerCase()
	
	type = getGhVersion().contains("pre") ? BETA : STABLE
	modLoaders.add(project.mod_loader.toLowerCase())
	additionalFiles.from(
		file("${project.buildDir}/libs/${archivesName}-${version}-sources.jar"),
	)
	
	curseforge {
		file = curseforgeJar.get().archiveFile
		projectId = project.cf_project_id
		accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
		minecraftVersions.add(project.cf_game_version)
		requires("fabric-api")
		changelog = getChangelog("CurseForge", "curseforge.com/minecraft/mc-mods/${modSlug}/version/${version}")
	}
	modrinth {
		file = modrinthJar.get().archiveFile
		projectId = project.mr_project_id
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersions.add(project.minecraft_version)
		requires("fabric-api")
		changelog = getChangelog("Modrinth", "modrinth.com/mod/${modSlug}/version/${version}")
	}
}

tasks.register("getCurseforgeId") {
	dependsOn publishCurseforge
	inputs.file publishCurseforge.result
	doLast {
		def result = new JsonSlurper().parseText(publishCurseforge.result.get().asFile.text)
		println "CURSEFORGE_FILE_ID=${result.fileId}"
	}
}

tasks.named("publishMods").configure {
	dependsOn tasks.named("build"), tasks.named("curseforgeJar"), tasks.named("modrinthJar")
}
tasks.named("publishCurseforge").configure {
	dependsOn tasks.named("build"), tasks.named("curseforgeJar")
}
tasks.named("publishModrinth").configure {
	dependsOn tasks.named("build"), tasks.named("modrinthJar")
}

import org.kohsuke.github.GHReleaseBuilder
import org.kohsuke.github.GitHub
tasks.register("github") {
	dependsOn wimodsJar, remapSourcesJar
	
	onlyIf {
		ENV.GITHUB_TOKEN
	}
	
	doLast {
		def github = GitHub.connectUsingOAuth(ENV.GITHUB_TOKEN as String)
		def repoId = project.mcx_repo_id
		def repository = github.getRepository(repoId)
		def ghVersion = getGhVersion()
		
		def ghRelease = repository.getReleaseByTagName(ghVersion as String)
		if(ghRelease == null) {
			def releaseBuilder = new GHReleaseBuilder(repository, ghVersion as String)
			releaseBuilder.body(getChangelogImage("github.com/${repoId}/releases/tag/${ghVersion}"))
			releaseBuilder.prerelease(ghVersion.contains("pre"))
			ghRelease = releaseBuilder.create()
		}
		
		ghRelease.uploadAsset(wimodsJar.get().archiveFile.get().getAsFile(), "application/java-archive")
		ghRelease.uploadAsset(remapSourcesJar.archiveFile.get().getAsFile(), "application/java-archive")
	}
}

import org.kohsuke.github.GHIssueState
import org.kohsuke.github.GHMilestoneState
import java.time.Instant
tasks.register("closeMilestone") {
	onlyIf {
		ENV.GITHUB_TOKEN
	}
	
	doLast {
		def github = GitHub.connectUsingOAuth(ENV.GITHUB_TOKEN as String)
		def repoId = project.gh_repo_id
		def repository = github.getRepository(repoId)
		def ghVersion = getGhVersion()
		
		// Weird API design: listMilestones() requires GHIssueState while everything else uses GHMilestoneState.
		def milestone = repository.listMilestones(GHIssueState.ALL).find { it.title == ghVersion }
		if (milestone == null) {
			milestone = repository.createMilestone(ghVersion, "")
		}
		
		if (milestone.getState() != GHMilestoneState.CLOSED) {
			milestone.setDueOn(Date.from(Instant.now()))
			milestone.setDescription(getChangelog("GitHub", "github.com/${repoId}/milestone/${ghVersion}"))
			milestone.close()
		}
	}
}

tasks.register("uploadBackups") {
	dependsOn remapJar, remapSourcesJar, curseforgeJar, modrinthJar, wimodsJar
	
	onlyIf {
		ENV.WI_BACKUPS_API_KEY
	}
	
	doLast {
		def shortVersion = getGhVersion().substring(1)
		def backupUrl = "https://api.wurstclient.net/artifact-backups/Mo-Glass/${shortVersion}"
		
		def maxRetries = 3
		def retryCount = 0
		def success = false
		
		while (!success && retryCount < maxRetries) {
			try {
				def connection = new URL(backupUrl).openConnection() as HttpURLConnection
				def boundary = UUID.randomUUID().toString()
				connection.setRequestMethod("POST")
				connection.setRequestProperty("X-API-Key", ENV.WI_BACKUPS_API_KEY)
				connection.setRequestProperty("Accept", "application/json")
				connection.setRequestProperty("Content-Type", "multipart/form-data; boundary=$boundary")
				connection.doOutput = true
				
				def output = connection.outputStream
				[remapJar, remapSourcesJar, curseforgeJar.get(), modrinthJar.get(), wimodsJar.get()].each { jarTask ->
					def file = jarTask.archiveFile.get().asFile
					output << "--${boundary}\r\n"
					output << "Content-Disposition: form-data; name=\"files\"; filename=\"${file.name}\"\r\n"
					output << "Content-Type: application/java-archive\r\n\r\n"
					file.withInputStream { input -> 
						output << input
					}
					output << "\r\n"
				}
				output << "--${boundary}--\r\n"
				output.flush()
				
				if(connection.responseCode != 200) {
					throw new IOException("HTTP ${connection.responseCode}: ${connection.responseMessage}")
				}
				
				success = true
				
			} catch (Exception e) {
				retryCount++
				if (retryCount >= maxRetries) {
					throw new GradleException("Failed to upload backups after ${maxRetries} attempts: ${e.message}")
				}
				println "Upload attempt ${retryCount} failed: ${e.message}. Retrying in 5 seconds..."
				Thread.sleep(5000)
			}
		}
	}
}
